prefix foaf: <http://xmlns.com/foaf/0.1/>
prefix nt: <https://w3id.org/np/o/ntemplate/>
prefix opo: <https://w3id.org/kpxl/opo/terms/>

select distinct ?person ?personLabel ?rel ?relLabel ?thing ?thingLabel where {
  ?person ?rel ?thing .
  values (?rel ?relLabel) { (opo:thinksIsOverrated "thinks is overrated") (opo:thinksIsUnderrated "thinks is underrated") } .
  ?thing nt:hasLabelFromApi ?thingLabelStr .
  bind(coalesce(replace(str(?thingLabelStr), " - .*", ""), replace(str(?thingLabelStr), "^.*/([^/]*)$", "$1")) as ?thingLabel)
  optional {
    ?person foaf:name ?personNameStr .
    bind(str(?personNameStr) as ?personLabel) 
  }
}
